buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'http://artifactory.playteam.ru/libs-release/' }
    }
    dependencies {
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.1"
    }
}

import io.gitlab.arturbosch.detekt.Detekt

apply plugin: io.gitlab.arturbosch.detekt.DetektPlugin

tasks.register("detektAll", Detekt) {
    description = "Runs over whole code base without the starting overhead for each module."
    parallel = true
    setSource(files(projectDir))
    include("**/*.kt")
    exclude("**/resources/**")
    exclude("**/build/**")
    config = files("$staticAnalysisDir/detekt-config.yml")
    baseline = file("$staticAnalysisDir/baseline.xml")
    ignoreFailures = true


    reports {
        xml {
            enabled = true
            destination = file("$staticAnalysisReportDir/report_detekt.xml")
        }
        html {
            enabled = false
        }
    }
}

detektBaseline {
    description = "Generate baseline file for project. Runs over whole code base without the starting overhead for each module."
    parallel = true
    setSource(files(projectDir))
    include("**/*.kt")
    include("**/*.kts")
    baseline = file("$staticAnalysisDir/baseline.xml")
    config = files("$staticAnalysisDir/detekt-config.yml")
    ignoreFailures = true
}